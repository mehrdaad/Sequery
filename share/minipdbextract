#!/bin/csh
#
# This script extracts the pdb lines for the residues which are 
#  outputted by sequery. It takes the start residue, end residue, pdbcode
#  from sequery output, searches the $PDBHOME database for that protein and
#  extracts pdblines from the files
#
#

# ***** CHANGE THIS LINE TO THE CORRECT PDB DATABASE LOCATION *****
set PDBHOME="/psa/pdb"
# *****************************************************************

if (${#argv} == 0 ) then
  echo " Extracts particular residue numbers from a pdb file."
  echo " Used to process output of sequery"
  echo " Usage: $0 sequery_output_file [x]"
  echo "                   sequery_output_file : file generated by sequery"
  echo "                   x : number of extra residues required (default 0)"
    exit
endif
  
set offset=0

if (${#argv} == 2) then
  set offset = ${argv[2]}
endif

foreach l ("`cat  $1`")
 
 set comment = "0"
 set comment = `echo $l|awk '$0~/^#/{print 1}'`
 if ("${comment}" == "1") then
   echo $l
   continue
 endif
 
 set info=`echo $l|nawk '{print $1,$2,$3,$5}'`
 set pdb   = $info[1]
 set id    = $info[2]
 set start = $info[3]
 set end   = $info[4]
 
 # update start and end position  
 @ start -= $offset
 @ end   += $offset
 
 
 if ( -e $PDBHOME/pdb${pdb}.ent) then
    # process if no chain ID is given
    if( "$id" == "_") then   
        nawk 'BEGIN {st = '"$start"'\
                     ed = '"$end"'} \
             $5 >= st && $5 <= ed && /^ATOM/ {print $0}' $PDBHOME/pdb${pdb}.ent
    else
    # process if chain ID is given
        nawk 'BEGIN { st  = '"${start}"'         \
                      ed  = '"${end}"'           \
                      cid = "'"${id}"'"         }\
            $6>=st && $6<=ed && /^ATOM/ && $5==cid {print $0}' $PDBHOME/pdb${pdb}.ent
    endif
        
 else
   echo "  ###### not in database ########"
 endif
 echo " "
end


